buildscript {
    ext.junit_version = '5.1.0'
}

plugins {
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.15.2'
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.72'
    id 'org.jetbrains.kotlin.kapt' version '1.3.72'
}

group = 'com.jeffpdavidson.kotwords'
version = '1.0.0-SNAPSHOT'

repositories {
    jcenter()
}

kotlin {
    jvm {
        withJava()

        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
        }

        compilations.test.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    js {
        browser { }
    }
}

dependencies {
    jvmMainImplementation "com.squareup.moshi:moshi:1.5.0"
    jvmMainImplementation "org.apache.pdfbox:pdfbox:2.0.9"
    jvmMainImplementation "org.glassfish:javax.json:1.1.2"
    jvmMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    jvmMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-nio:0.22.5"
    jvmMainImplementation "org.jetbrains.kotlinx:kotlinx-io-jvm:0.0.10"
    jvmMainImplementation "org.jsoup:jsoup:1.11.2"

    jvmMainImplementation "se.ansman.kotshi:api:1.0.4"
    kapt "se.ansman.kotshi:compiler:1.0.4"

    jvmTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    jvmTestRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    jsMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    jsMainImplementation(npm("jszip", "3.4.0"))

    jsTestImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

jvmTest {
    useJUnitPlatform()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    configuration {
        perPackageOption {
            prefix = "com.jeffpdavidson.kotwords.formats.json"
            suppress = true
        }
    }
}

task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    archiveClassifier.set("javadoc")
    from(dokka)
}

tasks.named("jvmSourcesJar") {
    exclude("com/jeffpdavidson/kotwords/formats/json")
}

publishing {
    publications {
        jvm {
            artifact(dokkaJar)
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.env.CI_DEPLOY_USERNAME
            password = System.env.CI_DEPLOY_PASSWORD
        }
        defaults {
            publications(publishing.publications.jvm, publishing.publications.js, publishing.publications.metadata)
        }
    }
}
